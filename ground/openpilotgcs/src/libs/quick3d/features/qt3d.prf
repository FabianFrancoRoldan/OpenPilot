!symbian {
    macx:CONFIG(qt_framework, qt_framework|qt_no_framework) {
        LIBS += -framework Qt3D
        # Makes includes like "#include <Qt3D/foo.h>" work in case of frameworks
        QMAKE_CXXFLAGS += -FQt3D
        # Makes includes like "#include <foo.h>" work in case of frameworks
        INCLUDEPATH += $$[QT_INSTALL_LIBS]/Qt3D.framework/Versions/1/Headers
    } else {
        win32:CONFIG(debug, debug|release) {
            LIBS += -lQt3D$${QT_LIBINFIX}d
        } else {
            LIBS += -lQt3D$${QT_LIBINFIX}
        }
    }
    INCLUDEPATH += $$[QT_INSTALL_HEADERS]/Qt3D
} else {
    load(data_caging_paths)

    INCLUDEPATH+=$$MW_LAYER_PUBLIC_EXPORT_PATH(Qt3D)
    LIBS += -lQt3D$${QT_LIBINFIX}
}

QT += opengl network
